apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.dependency.graph.generator' /* optional */

buildscript {
  ext.versions = [
    espresso: '3.5.1',
    testRules: '1.0.2',
    kotlin: '1.8.10',
  ]

  ext.deps = [
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
    ],
    'support': [
      'test': [
        'espresso': [
          'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
        ],
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
        'autoService': "com.google.auto.service:auto-service:1.0-rc4",
      ],
      'material': "com.google.android.material:material:1.2.1",
    ],
    'fastLaneScreenGrab': "tools.fastlane:screengrab:1.2.0",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.22.0'
    classpath 'com.vanniktech:gradle-dependency-graph-generator-plugin:0.8.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'

    classpath 'com.android.tools.build:gradle:7.4.2'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.26.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

codeQualityTools {
  lint {
    lintConfig = rootProject.file("lint.xml")
    checkAllWarnings = true
    textReport = true
  }
  checkstyle {
    enabled = false // Kotlin only.
  }
  pmd {
    enabled = false // Kotlin only.
  }
  ktlint {
    toolVersion = '0.47.1'
    experimental = true
  }
  detekt {
    enabled = false
  }
  cpd {
    enabled = false
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
  }

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:deprecation"
      options.compilerArgs << "-Xlint:unchecked"
      options.compilerArgs << "-Xmaxwarns" << "1000"
      options.compilerArgs << "-Xmaxerrs" << "1000"
    }
  }
}

ext {
  minSdkVersion = 16
  compileSdkVersion = 33
  targetSdkVersion = 33

  leakCanaryVersion = '2.10'

  versionCode = VERSION_CODE
  versionName = VERSION_NAME

  supportAppCompat = "androidx.appcompat:appcompat:1.1.0"
  supportV4CoreUi = "androidx.legacy:legacy-support-core-ui:1.0.0"
  supportV4Compat = "androidx.core:core:1.7.0"
  supportRecyclerView = "androidx.recyclerview:recyclerview:1.1.0"
  supportCardView = "androidx.cardview:cardview:1.0.0"
  supportAnnotations = "androidx.annotation:annotation:1.0.0"
  supportEmoji = "androidx.emoji:emoji-appcompat:1.0.0"

  leakCanaryDebug = "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

  testing = [
          junit             : 'junit:junit:4.13.2',
          robolectric       : 'org.robolectric:robolectric:4.3.1',
          privateConstructor: 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
          assertJ           : 'org.assertj:assertj-core:2.9.1',
          autoService       : 'com.google.auto.service:auto-service:1.0-rc4'
  ]

  javaVersion = JavaVersion.VERSION_1_8
}

configurations.all {
  resolutionStrategy {
    force 'com.google.android.material:material:1.2.1'
    force 'androidx.appcompat:appcompat:1.1.0'
  }
}