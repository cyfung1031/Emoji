apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    espresso: '3.5.1',
    testRules: '1.0.2',
    kotlin: '1.4.32',
  ]

  ext.deps = [
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
    ],
    'support': [
      'test': [
        'espresso': [
          'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
        ],
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
        'autoService': "com.google.auto.service:auto-service:1.0-rc4",
      ],
      'material': "com.google.android.material:material:1.1.0-beta01",
    ],
    'fastLaneScreenGrab': "tools.fastlane:screengrab:1.2.0",
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.22.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'

    classpath 'com.android.tools.build:gradle:3.6.4'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.26.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

codeQualityTools {
  lint {
    lintConfig = rootProject.file("lint.xml")
    checkAllWarnings = true
    textReport = true
  }
  checkstyle {
    enabled = false // Kotlin only.
  }
  pmd {
    enabled = false // Kotlin only.
  }
  ktlint {
    toolVersion = '0.34.2'
    experimental = true
  }
  detekt {
    enabled = false
  }
  cpd {
    enabled = false
  }
}

junitJacoco {
  // Don't care about sample and generated modules
  ignoreProjects = ['app', 'emoji-ios', 'emoji-google', 'emoji-twitter', 'emoji-compat', 'emoji-kotlin']
}

subprojects {
  repositories {
    google()
    mavenCentral()
  }

  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:deprecation"
    }
  }
}

ext {
  minSdkVersion = 16
  compileSdkVersion = 33
  targetSdkVersion = 33

  leakCanaryVersion = '2.10'

  versionCode = VERSION_CODE
  versionName = VERSION_NAME

  supportAppCompat = "androidx.appcompat:appcompat:1.1.0"
  supportV4CoreUi = "androidx.legacy:legacy-support-core-ui:1.0.0"
  supportV4Compat = "androidx.core:core:1.7.0"
  supportRecyclerView = "androidx.recyclerview:recyclerview:1.1.0"
  supportCardView = "androidx.cardview:cardview:1.0.0"
  supportAnnotations = "androidx.annotation:annotation:1.0.0"
  supportEmoji = "androidx.emoji:emoji-appcompat:1.0.0"

  leakCanaryDebug = "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

  testing = [
          junit             : 'junit:junit:4.13.2',
          robolectric       : 'org.robolectric:robolectric:4.3.1',
          privateConstructor: 'com.pushtorefresh.java-private-constructor-checker:checker:1.2.0',
          assertJ           : 'org.assertj:assertj-core:2.9.1',
          autoService       : 'com.google.auto.service:auto-service:1.0-rc4'
  ]

  javaVersion = JavaVersion.VERSION_1_8
}

wrapper {
  gradleVersion = '5.6.2'
  distributionType = Wrapper.DistributionType.ALL
}

subprojects {
  project.plugins.withId("com.android.library") {
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    project.android.libraryVariants.all {
      it.generateBuildConfigProvider.configure { it.enabled = false }
    }
  }
}
